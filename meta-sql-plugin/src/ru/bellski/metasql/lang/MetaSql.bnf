{

 parserClass="ru.bellski.metasql.lang.parser.MetaSqlParser"
 parserUtilClass="ru.bellski.metasql.lang.parser.MetaSqlParserUtil"


    implements = "ru.bellski.metasql.lang.psi.MetaSqlElement"
    extends = "ru.bellski.metasql.lang.psi.impl.MetaSqlCompositeElementImpl"

    psiClassPrefix="MetaSql"

    psiImplClassSuffix="Impl"

    psiPackage="ru.bellski.metasql.lang.psi"

    psiImplPackage="ru.bellski.metasql.lang.psi.impl"

    elementTypeHolderClass="ru.bellski.metasql.lang.psi.MetaSqlTypes"
    elementTypeClass="ru.bellski.metasql.lang.psi.MetaSqlElementType"
    tokenTypeClass="ru.bellski.metasql.lang.psi.MetaSqlTokenType"

    psiImplUtilClass="ru.bellski.metasql.util.MetaSqlUtils"

 tokens=[
     ANY='any';
     SEMI=';'
     EQ='='

     METADATA="Metadata"
     RETURNRULE="ReturnRule"

     LIST="List"
     BOOLEAN="Boolean"
     INTEGER="Integer"
     SINGLE="Single"

     M_IDENTIFIER="m_identifier"
   ]

}

root ::= metaSqlBody

metaSqlBody ::= clauseList?
{mixin="ru.bellski.metasql.lang.psi.impl.MetaSqlBodyImpl" implements="ru.bellski.metasql.lang.psi.MetaSqlBody" recoverWhile=metaSqlBody_recovery}
private metaSqlBody_recovery ::= !';'

private clauseList ::= returnRuleClause metadataClause? params?

returnRuleClause ::= 'ReturnRule' returnRuleInit ';'
{pin=1 mixin="ru.bellski.metasql.lang.psi.impl.ReturnRuleClauseImpl" implements="ru.bellski.metasql.lang.psi.ReturnRuleClause"}
metadataClause ::= 'Metadata' metadataInit ';'
{pin=1 mixin="ru.bellski.metasql.lang.psi.impl.MetadataClauseImpl" implements="ru.bellski.metasql.lang.psi.MetadataClause"}


returnRuleInit ::= '=' returnRuleValue {pin=1 recoverWhile=returnResultInit_recovery}
returnRuleValue ::= 'List' | 'Boolean' | 'Integer' | 'Single'
{mixin="ru.bellski.metasql.lang.psi.impl.ReturnRuleValueImpl" implements="ru.bellski.metasql.lang.psi.ReturnRuleValue"}
private returnResultInit_recovery ::= !(';' | 'Metadata' '=')

metadataInit ::= '=' metadataReference {pin=1 recoverWhile=metadataInit_recovery}
metadataReference ::= 'm_identifier'
{mixin="ru.bellski.metasql.lang.psi.impl.MetadataReferenceImpl" implements="ru.bellski.metasql.lang.psi.MetadataReference"}
private metadataInit_recovery ::= !';'

private params ::= param*
param ::= 'm_identifier'















